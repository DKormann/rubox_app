WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ "//" ~ (!"\n" ~ ANY)* }

ident      = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
int        = @{ "-"? ~ ASCII_DIGIT+ }
float      = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }
string     = @{ "\"" ~ ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" }
string2    = @{ "\'" ~ ( "\\\'" | (!"\'" ~ ANY) )* ~ "\'" }


boolean    = { "true" | "false" }
null       = { "null" }
undefined  = { "undefined" }

literal    = _{ float | int | string | string2 | boolean | null | undefined }

params     = { "(" ~ (ident ~ ("," ~ ident)*)? ~ ")" }
arglist    = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }
prop       = { ( ident | string ) ~ ":" ~ expr }

spread     = { "..." ~ expr }
arrayItem  = { expr | spread }

array      = { "[" ~ (arrayItem ~ ("," ~ arrayItem)* ~ (",")?)? ~ "]" }
object     = { "{" ~ (prop ~ ("," ~ prop)* ~ (",")?)? ~ "}" }

fun        = { params ~ "=>" ~ (block | expr) }

primary    = _{ literal | ident | array | object | "(" ~ expr ~ ")" | unop }

field = {"." ~ ident}
index = {"[" ~ expr ~ "]"}
access_chain = { primary ~ (field | index | arglist)+ }

operand    = { access_chain | primary }
op         = { "==" | "!=" | ">=" | "<=" | "+" | "-" | "*" | "/" | ">" | "<" | "||" | "&&" | "??" }
binop      = { operand ~ op ~ operand }

uop        = { "!" | "-" }
unop       = { uop ~ operand }

cond       = { (binop | operand) ~ "?" ~ expr ~ ":" ~ expr }

expr       = _{ ( fun | cond | binop | access_chain | primary ) }

let_       = { "let" ~ ident ~ "=" ~ expr ~ ";" }

expr_      = { expr ~ ( ";" | "," ) }

function_  = { "function" ~ ident ~ params ~ block }



stmt       = { return_ | let_ | expr_ | function_ | if_ }

return_    = { "return" ~ expr ~ ";"? }

block      = { "{" ~ stmt* ~ "}"} 

if_        = { "if" ~ "(" ~ expr ~ ")" ~ block ~ ("else" ~ (if_ | block))? }





program    = { SOI ~ expr ~ EOI }